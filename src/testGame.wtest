import jefe.*
import jugador.*
import oficina.*
import direcciones.*
import wollok.game.*
import pantallaJuego.*
import configuraciones.*
import objetivos.*
import sonidos.*
import tarjetas.*
import menuInicio.*


describe "Jugador en la primera fase"{
	const tareasNecesarias = #{tareaAzul, tareaRojo, tareaVerde}
	
	fixture {
		game.clear()
		sonido.escapeBoss(mockGame)
		game.addVisual(jugador)
		game.addVisual(tareaAzul)
		game.addVisual(tareaRojo)
		game.addVisual(tareaVerde)
		game.addVisual(companieriAzul)
		game.addVisual(companieriRojo)
		game.addVisual(companieriVerde)
		game.addVisual(impresoraRojo)
		game.addVisual(impresoraAzul)
		game.addVisual(impresoraVerde)
		game.addVisual(ascensor2)
		game.addVisual(jefe1)
		jefe1.elegirDificultad(facil)
		jefe2.elegirDificultad(facil)
		jefe1.moverse()
		game.onTick(10000, "Sacar tarjeta", {})
	}

	test"El jugador pierde energia al moverse"{
		jugador.moverse(arriba)
		jugador.moverse(derecha)
		assert.equals(96,jugador.energia())
	}
	
	test "El jugador interactua con un consumible" {
		game.addVisual(cafeConLeche)
		jugador.disminuirEnergia(30)
		cafeConLeche.interactuar()
		assert.equals(95,jugador.energia())
	}

	test"El jugador puede atravesar por la puerta si tiene todas las tareas realizadas"{
		jugador.terminarTarea(tareaAzul)
		jugador.terminarTarea(tareaRojo)
		jugador.terminarTarea(tareaVerde)	
		assert.equals(tareasNecesarias, jugador.tareasRealizadas())
		assert.that(puerta.avanzar())
	}

	test"El jugador no puede atravesar por la puerta si no tiene todas las tareas realizadas"{
		jugador.terminarTarea(tareaAzul)
		assert.notThat(puerta.avanzar())
	}

	test"El jugador obtiene una copia"{
		assert.that(impresoraAzul.puedeImprimir())
		impresoraAzul.interactuar()
		assert.equals(tareaAzul, jugador.tareaEnMano())
	}

	test"El jugador no puede volver a tomar la misma tarea"{
		assert.that(impresoraVerde.puedeImprimir())
		impresoraVerde.interactuar()
		assert.equals(tareaVerde, jugador.tareaEnMano())
		jugador.terminarTarea(tareaVerde)
		assert.notThat(impresoraVerde.puedeImprimir())
	}

	test"El jugador le entrega la tarea al compañero"{
		impresoraAzul.interactuar()
		companieriAzul.interactuar()
		assert.that(companieriAzul.tareaEntregada())
	}

	test"El jugador entrega una tarea equivocada al compañero"{
		impresoraVerde.interactuar()
		companieriRojo.interactuar()
		assert.notThat(companieriRojo.tareaEntregada())
	}

}


describe "Jugador en la segunda fase"{
	
	test"El jugador agarra un cafe de la maquina"{
		maquinaCafe.interactuar()
		assert.equals(cafe, jugador.objetoEnMano())
	}
	
	test"El jugador agarra el helado de la heladera"{
		heladera.interactuar()
		assert.equals(helado, jugador.objetoEnMano())
	}
	
	test"El jugador le entrega el café a la compañera Verde"{
		const companieraVerde = new Companieri(color = "Verde", tareaRequerida = tareaVerde, producto = cafe, pedido = pedidoVerde, position = game.at(5,8))
		game.addVisual(companieraVerde)
		maquinaCafe.interactuar()
		companieraVerde.interactuar()
		assert.equals(vacio, jugador.objetoEnMano())
		assert.that(companieraVerde.objetoEntregado())
	}
	
	test"El jugador le entrega un helado a la compañera Verde"{
		const companieraAzul = new Companieri(color = "Verde", tareaRequerida = tareaVerde, producto = helado, pedido = pedidoVerde, position = game.at(5,8))
		game.addVisual(companieraAzul)
		heladera.interactuar()
		companieraAzul.interactuar()
		assert.equals(vacio,jugador.objetoEnMano())
		assert.that(companieraAzul.objetoEntregado())
	}
	
	test"El jugador no puede entregarle café a un compañero que quiere helado"{
		const companieraRoja = new Companieri(color = "Verde", tareaRequerida = tareaVerde, producto = helado, pedido = pedidoVerde, position = game.at(5,8))
		game.addVisual(companieraRoja)
		maquinaCafe.interactuar()
		companieraRoja.interactuar()
		assert.notThat(companieraRoja.tareaEntregada())
	}
}

describe"Jugador en la tercera fase"{
	
	fixture{
		game.addVisual(jugador)
		game.addVisual(mochila)
		game.addVisual(celular)
		game.addVisual(billetera)
		game.addVisual(llaves)
		game.addVisual(credencial)
	}
	test"El jugador agarra la mochila"{
		mochila.interactuar()
		assert.that(jugador.tieneMochila())
	}
	test"El jugador sin la mochila,no puede agarrar un objeto"{
		celular.interactuar()
		assert.notThat(celular.guardado())
	}
	test"El jugador con la mochila,puede agarrar un objeto"{
		mochila.interactuar()
		billetera.interactuar()
		assert.that(billetera.guardado())
	}
}


describe "Tarjetas del juego" {
	const impresoras = [impresoraAzul, impresoraRojo, impresoraVerde]
	const posImpresoras = impresoras.map({impresora => impresora.position()})
	const companieris = [companieriAzul, companieriRojo, companieriVerde]
	const posCompanieris = companieris.map({companieri => companieri.position()})
	
	fixture {
		game.addVisual(jugador)
		game.addVisual(mochila)
		game.addVisual(llaves)
		game.addVisual(billetera)
		game.addVisual(celular)
		game.addVisual(credencial)
	}
	test "El jefe1 reaparece en una impresora" {
		jefeAUnaImpresora.producirEfecto()
		assert.that(posImpresoras.contains(jefe1.position()))
	}
	
	test "El jefe 2 reaparece en el cubiculo de un companieri" {
		jefe2ACompanieri.producirEfecto()
		assert.that(posCompanieris.contains(jefe2.position()))
	}
	
	test "El jugador recibe un bonus de energia" {
		jugador.energia(70)
		agregarEnergia.producirEfecto()
		assert.equals(85, jugador.energia())
	}
	
	test "El jugador restaura por completo su energia" {
		jugador.energia(20)
		restaurarEnergia.producirEfecto()
		assert.equals(100, jugador.energia())
	}
	
	test "El jugador pierde un porcentaje de su energia" {
		jugador.energia(60)
		perderEnergia.producirEfecto()
		assert.equals(51, jugador.energia())
	}
	
	test "El jugador pierde una de las tareas hechas" {
		jugador.terminarTarea(tareaAzul)
		jugador.terminarTarea(tareaRojo)
		jugador.terminarTarea(tareaVerde)	
		assert.equals(3, jugador.tareasRealizadas().size())
		perderTarea.producirEfecto()
		assert.equals(2, jugador.tareasRealizadas().size())
	}
	
	test "El jugador vuelve a su posición inicial" {
		jugador.position(game.at(5,3))
		volverAlInicio.producirEfecto()
		assert.equals(game.at(3,1), jugador.position())
	}
	
	test "El jugador pierde un objeto de la mochila" {
		mochila.interactuar()
		billetera.interactuar()
		celular.interactuar()
		credencial.interactuar()
		llaves.interactuar()
		assert.equals(4, jugador.objetosEnMochila().size())
		perderObjeto.producirEfecto()
		assert.equals(3, jugador.objetosEnMochila().size())
	}
}
