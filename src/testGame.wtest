import jefe.*
import jugador.*
import oficina.*
import direcciones.*
import wollok.game.*
import pantallaJuego.*
import configuraciones.*
import tareas.*
import sonidos.*
import tarjetas.*
import menuInicio.*


describe "Jugador en la primera fase"{
	const tareasNecesarias = #{tareaAzul, tareaRojo, tareaVerde}
	
	fixture {
		game.clear()
		sonido.escapeBoss(mockGame)
		game.addVisual(jugador)
		game.addVisual(tareaAzul)
		game.addVisual(tareaRojo)
		game.addVisual(tareaVerde)
		game.addVisual(companieriAzul)
		game.addVisual(companieriRojo)
		game.addVisual(companieriVerde)
		game.addVisual(impresoraRojo)
		game.addVisual(impresoraAzul)
		game.addVisual(impresoraVerde)
		game.addVisual(puerta)
		game.addVisual(jefe1)
		jefe1.elegirDificultad(facil)
		jefe1.moverse()
	}

	test"El jugador pierde energia al moverse"{
		jugador.moverse(arriba)
		jugador.moverse(derecha)
		assert.equals(94,jugador.energia())
	}
	
	test "El jugador interactua con un consumible" {
		game.addVisual(cafeConLeche)
		jugador.disminuirEnergia(30)
		cafeConLeche.interactuar()
		assert.equals(95,jugador.energia())
	}

	test"El jugador puede atravesar por la puerta si tiene todas las tareas realizadas"{
		jugador.terminarTarea(tareaAzul)
		jugador.terminarTarea(tareaRojo)
		jugador.terminarTarea(tareaVerde)	
		assert.equals(tareasNecesarias, jugador.tareasRealizadas())
		assert.that(puerta.avanzar())
	}

	test"El jugador no puede atravesar por la puerta si no tiene todas las tareas realizadas"{
		jugador.terminarTarea(tareaAzul)
		assert.notThat(puerta.avanzar())
	}

	test"El jugador obtiene una tarea"{
		assert.that(impresoraAzul.puedeImprimir())
		impresoraAzul.interactuar()
		assert.equals(tareaAzul, jugador.tareaEnMano())
	}

	test"El jugador no puede volver a tomar la misma tarea"{
		assert.that(impresoraVerde.puedeImprimir())
		impresoraVerde.interactuar()
		assert.equals(tareaVerde, jugador.tareaEnMano())
		jugador.terminarTarea(tareaVerde)
		assert.notThat(impresoraVerde.puedeImprimir())
	}

	test"El jugador le entrega la tarea al compañero"{
		impresoraAzul.interactuar()
		assert.that(companieriAzul.interactuar())
	}

	test"El jugador entrega una tarea equivocada al compañero"{
		impresoraVerde.interactuar()
		assert.notThat(companieriRojo.interactuar())
	}

}
